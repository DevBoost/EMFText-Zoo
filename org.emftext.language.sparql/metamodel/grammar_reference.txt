//Quelle: http://www.w3.org/TR/rdf-sparql-query/#grammar
//pre-done
[1]    	Query  	  ::=    	Prologue
						( SelectQuery | ConstructQuery | DescribeQuery | AskQuery )
	//multiple Queries im Metamodell vorgesehen, Grammatik sieht aber nur EINE vor?

//done	
[2]   	Prologue 	  ::=   	BaseDecl? PrefixDecl*
//done
[3]   	BaseDecl 	  ::=   	'BASE' IRI_REF
//done
[4]   	PrefixDecl 	  ::=   	'PREFIX' PNAME_NS IRI_REF
//done
[5]   	SelectQuery 	  ::=   	'SELECT' ( 'DISTINCT' | 'REDUCED' )? ( Var+ | '*' ) DatasetClause* WhereClause SolutionModifier
//done
[6]   	ConstructQuery 	  ::=   	'CONSTRUCT' ConstructTemplate DatasetClause* WhereClause SolutionModifier
//done
[7]   	DescribeQuery 	  ::=   	'DESCRIBE' ( VarOrIRIref+ | '*' ) DatasetClause* WhereClause? SolutionModifier
//done
[8]   	AskQuery 	  ::=   	'ASK' DatasetClause* WhereClause
//done
[9]   	DatasetClause 	  ::=   	'FROM' ( DefaultGraphClause | NamedGraphClause )
//done
[10]   	DefaultGraphClause 	  ::=   	SourceSelector
//done
[11]   	NamedGraphClause 	  ::=   	'NAMED' SourceSelector
//done as abstract
[12]   	SourceSelector 	  ::=   	IRIref
//done
[13]   	WhereClause 	  ::=   	'WHERE'? GroupGraphPattern
//done
[14]   	SolutionModifier 	  ::=   	OrderClause? LimitOffsetClauses?
//done
[15]   	LimitOffsetClauses 	  ::=   	( LimitClause OffsetClause? | OffsetClause LimitClause? )
//done
[16]   	OrderClause 	  ::=   	'ORDER' 'BY' OrderCondition+
//done
[17]   	OrderCondition 	  ::=   	( ( 'ASC' | 'DESC' ) BrackettedExpression )
									| ( Constraint | Var )
//done									
[18]   	LimitClause 	  ::=   	'LIMIT' INTEGER
//done
[19]   	OffsetClause 	  ::=   	'OFFSET' INTEGER

//done with AdditionalGGPElement
[20]   	GroupGraphPattern 	  ::=   	'{' TriplesBlock? ( ( GraphPatternNotTriples | Filter ) '.'? TriplesBlock? )* '}'
//done
[21]   	TriplesBlock 	  ::=   	TriplesSameSubject ( '.' TriplesBlock? )?
//done
[22]   	GraphPatternNotTriples 	  ::=   	OptionalGraphPattern | GroupOrUnionGraphPattern | GraphGraphPattern
//done
[23]   	OptionalGraphPattern 	  ::=   	'OPTIONAL' GroupGraphPattern
//done
[24]   	GraphGraphPattern 	  ::=   	'GRAPH' VarOrIRIref GroupGraphPattern
//done
[25]   	GroupOrUnionGraphPattern 	  ::=   	GroupGraphPattern ( 'UNION' GroupGraphPattern )*
//done
[26]   	Filter 	  ::=   	'FILTER' Constraint
//done
[27]   	Constraint 	  ::=   	BrackettedExpression | BuiltInCall | FunctionCall
//done
[28]   	FunctionCall 	  ::=   	IRIref ArgList

//done
[29]   	ArgList 	  ::=   	( NIL | '(' Expression ( ',' Expression )* ')' )

//done
[30]   	ConstructTemplate 	  ::=   	'{' ConstructTriples? '}'
//done in ConstructTemplate
[31]   	ConstructTriples 	  ::=   	TriplesSameSubject ( '.' ConstructTriples? )?
//done
[32]   	TriplesSameSubject 	  ::=   	VarOrTerm PropertyListNotEmpty | TriplesNode PropertyList
//done
[33]   	PropertyListNotEmpty 	  ::=   	Verb ObjectList ( ';' ( Verb ObjectList )? )*
//done in TriplesSameSubjectRightNE
[34]   	PropertyList 	  ::=   	PropertyListNotEmpty?
//done
[35]   	ObjectList 	  ::=   	Object ( ',' Object )*
//done. könnte Abstract sein
[36]   	Object 	  ::=   	GraphNode
// done
[37]   	Verb 	  ::=   	VarOrIRIref | 'a'
//done
[38]   	TriplesNode 	  ::=   	Collection | BlankNodePropertyList
//done
[39]   	BlankNodePropertyList 	  ::=   	'[' PropertyListNotEmpty ']'
//done
[40]   	Collection 	  ::=   	'(' GraphNode+ ')'
//done
[41]   	GraphNode 	  ::=   	VarOrTerm | TriplesNode
//done
[42]   	VarOrTerm 	  ::=   	Var | GraphTerm
//done
[43]   	VarOrIRIref 	  ::=   	Var | IRIref
//done as Token, elementar
[44]   	Var 	  ::=   	VAR1 | VAR2
//done
[45]   	GraphTerm 	  ::=   	IRIref | RDFLiteral | NumericLiteral | BooleanLiteral | BlankNode | NIL

//done, genau wie AdditionalExpressionNE
[46]   	Expression 	  ::=   	ConditionalOrExpression
//done
[47]   	ConditionalOrExpression 	  ::=   	ConditionalAndExpression ( '||' ConditionalAndExpression )*
//done
[48]   	ConditionalAndExpression 	  ::=   	ValueLogical ( '&&' ValueLogical )*
//done
[49]   	ValueLogical 	  ::=   	RelationalExpression

//done
[50]   	RelationalExpression 	  ::=   	NumericExpression ( '=' NumericExpression | '!=' NumericExpression | '<' NumericExpression | '>' NumericExpression | '<=' NumericExpression | '>=' NumericExpression )?
//done
[51]   	NumericExpression 	  ::=   	AdditiveExpression
//done
[52]   	AdditiveExpression 	  ::=   	MultiplicativeExpression ( '+' MultiplicativeExpression | '-' MultiplicativeExpression | NumericLiteralPositive | NumericLiteralNegative )*
//done
[53]   	MultiplicativeExpression 	  ::=   	UnaryExpression ( '*' UnaryExpression | '/' UnaryExpression )*
//done
[54]   	UnaryExpression 	  ::=   	'!' PrimaryExpression
										| '+' PrimaryExpression
										| '-' PrimaryExpression
										| PrimaryExpression
//done										
[55]   	PrimaryExpression 	  ::=   	BrackettedExpression | BuiltInCall | IRIrefOrFunction | RDFLiteral | NumericLiteral | BooleanLiteral | Var
//done
[56]   	BrackettedExpression 	  ::=   	'(' Expression ')'
//done
[57]   	BuiltInCall 	  ::=   	  'STR' '(' Expression ')'
				| 'LANG' '(' Expression ')'
				| 'LANGMATCHES' '(' Expression ',' Expression ')'
				| 'DATATYPE' '(' Expression ')'
				| 'BOUND' '(' Var ')'
				| 'sameTerm' '(' Expression ',' Expression ')'
				| 'isIRI' '(' Expression ')'
				| 'isURI' '(' Expression ')'
				| 'isBLANK' '(' Expression ')'
				| 'isLITERAL' '(' Expression ')'
				| RegexExpression
//done
[58]   	RegexExpression 	  ::=   	'REGEX' '(' Expression ',' Expression ( ',' Expression )? ')'

//done
[59]   	IRIrefOrFunction 	  ::=   	IRIref ArgList?
//done
[60]   	RDFLiteral 	  ::=   	String ( LANGTAG | ( '^^' IRIref ) )?

//done, direkter Verweis durch INTEGER | DECIMAL | DOUBLE 
[61]   	NumericLiteral 	  ::=   	NumericLiteralUnsigned | NumericLiteralPositive | NumericLiteralNegative
//überflüssig, da durch NumericLiteral abgedeckt
[62]   	NumericLiteralUnsigned 	  ::=   	INTEGER | DECIMAL | DOUBLE
//überflüssig, da durch NumericLiteral abgedeckt
[63]   	NumericLiteralPositive 	  ::=   	INTEGER_POSITIVE | DECIMAL_POSITIVE | DOUBLE_POSITIVE
//überflüssig, da durch NumericLiteral abgedeckt
[64]   	NumericLiteralNegative 	  ::=   	INTEGER_NEGATIVE | DECIMAL_NEGATIVE | DOUBLE_NEGATIVE

//done in True/FalseBooleanLiteralNE
[65]   	BooleanLiteral 	  ::=   	'true' | 'false'

//done as StringLiteral; nur "String" ergibt Konflike beim generieren des Sourcecodes
[66]   	String 	  ::=   	STRING_LITERAL1 | STRING_LITERAL2 | STRING_LITERAL_LONG1 | STRING_LITERAL_LONG2
//done
[67]   	IRIref 	  ::=   	IRI_REF | PrefixedName
//done
[68]   	PrefixedName 	  ::=   	PNAME_LN | PNAME_NS
//done
[69]   	BlankNode 	  ::=   	BLANK_NODE_LABEL | ANON


Productions for terminals:
//done as token, zugelassen sind alle Zeichen außer die hier aufgeführten ("^" scheint einem "~" zu entsprechen), Hexcode als Unicode, elementar
//genaue Bedeutung der vorgegebenen Syntax aber unklar 
[70]   	IRI_REF 	  ::=   	'<' ([^<>"{}|^`\]-[#x00-#x20])* '>'
//done as Token, elementar
[71]   	PNAME_NS 	  ::=   	PN_PREFIX? ':'
//done
[72]   	PNAME_LN 	  ::=   	PNAME_NS PN_LOCAL
//done
[73]   	BLANK_NODE_LABEL 	  ::=   	'_:' PN_LOCAL

//überflüssig, in Token VARNAME
[74]   	VAR1 	  ::=   	'?' VARNAME
//überflüssig, in Token VARNAME
[75]   	VAR2 	  ::=   	'$' VARNAME

//done as token ,elementar
[76]   	LANGTAG 	  ::=   	'@' [a-zA-Z]+ ('-' [a-zA-Z0-9]+)*

//done as token, elementar
[77]   	INTEGER 	  ::=   	[0-9]+
//done as token, elementar
[78]   	DECIMAL 	  ::=   	[0-9]+ '.' [0-9]* | '.' [0-9]+
//done as token, elementar
[79]   	DOUBLE 	  ::=   	[0-9]+ '.' [0-9]* EXPONENT | '.' ([0-9])+ EXPONENT | ([0-9])+ EXPONENT

//done in INTEGER
[80]   	INTEGER_POSITIVE 	  ::=   	'+' INTEGER
//done in DECIMAL
[81]   	DECIMAL_POSITIVE 	  ::=   	'+' DECIMAL
//done in DOUBLE
[82]   	DOUBLE_POSITIVE 	  ::=   	'+' DOUBLE
//done in INTEGER
[83]   	INTEGER_NEGATIVE 	  ::=   	'-' INTEGER
 //done in DECIMAL
[84]   	DECIMAL_NEGATIVE 	  ::=   	'-' DECIMAL
//done in DOUBLE
[85]   	DOUBLE_NEGATIVE 	  ::=   	'-' DOUBLE
//done in DOUBLE
[86]   	EXPONENT 	  ::=   	[eE] [+-]? [0-9]+

//Syntax der Strings nicht eindeutig klar; überlappung der Definitionen
//done as token, elementar
[87]   	STRING_LITERAL1 	  ::=   	"'" ( ([^#x27#x5C#xA#xD]) | ECHAR )* "'"
//done as token, elementar
[88]   	STRING_LITERAL2 	  ::=   	'"' ( ([^#x22#x5C#xA#xD]) | ECHAR )* '"'
//done as token, elementar
[89]   	STRING_LITERAL_LONG1 	  ::=   	"'''" ( ( "'" | "''" )? ( [^'\] | ECHAR ) )* "'''"
//done as token, elementar
[90]   	STRING_LITERAL_LONG2 	  ::=   	'"""' ( ( '"' | '""' )? ( [^"\] | ECHAR ) )* '"""'
//done in Strings
[91]   	ECHAR 	  ::=   	'\' [tbnrf\"']

//done
[92]   	NIL 	  ::=   	'(' WS* ')'
//done as Token in WHITESPACE
[93]   	WS 	  ::=   	#x20 | #x9 | #xD | #xA
//done
[94]   	ANON 	  ::=   	'[' WS* ']'

//pre-done as Token, in VARNAME, PN_PREFIX und PN_LOCAL eingearbeitet, Hexcode als Unicode, [#x10000-#xEFFFF] lässt sich bisher nicht realisieren
[95]   	PN_CHARS_BASE ::=   	[A-Z] | [a-z] | [#x00C0-#x00D6] | [#x00D8-#x00F6] | [#x00F8-#x02FF] | [#x0370-#x037D] | [#x037F-#x1FFF] | [#x200C-#x200D] | [#x2070-#x218F] | [#x2C00-#x2FEF] | [#x3001-#xD7FF] | [#xF900-#xFDCF] | [#xFDF0-#xFFFD] | [#x10000-#xEFFFF]

//done as Token, in VARNAME, PN_PREFIX und PN_LOCAL eingearbeitet
[96]   	PN_CHARS_U 	  ::=   	PN_CHARS_BASE | '_'

//pre-done in Var, Hexcode als Unicode
[97]   	VARNAME 	  ::=   	( PN_CHARS_U | [0-9] ) ( PN_CHARS_U | [0-9] | #x00B7 | [#x0300-#x036F] | [#x203F-#x2040] )*

//done as Token, in VARNAME, PN_PREFIX und PN_LOCAL eingearbeitet, Hexcode als Unicode
[98]   	PN_CHARS 	  ::=   	PN_CHARS_U | '-' | [0-9] | #x00B7 | [#x0300-#x036F] | [#x203F-#x2040]

//pre-done in PNAME_NS
[99]   	PN_PREFIX 	  ::=   	PN_CHARS_BASE ((PN_CHARS|'.')* PN_CHARS)?

//pre-done as Token, elementar
[100]   PN_LOCAL 	  ::=   	( PN_CHARS_U | [0-9] ) ((PN_CHARS|'.')* PN_CHARS)?
